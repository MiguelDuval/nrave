import "." as Skin
import Mixxx 1.0 as Mixxx
import QtQuick 2.12
import QtQuick.Controls
import QtQuick.Window
import "Theme"

ApplicationWindow {
    id: root

    readonly property bool isMobile: Qt.platform.os === "android" || Qt.platform.os === "ios"
    readonly property int designWidth: 1792
    readonly property int designHeight: 1008
    property var bitGridOverlay: bitgridOverlay.item

    color: Theme.backgroundColor
    height: isMobile ? Screen.height : designHeight
    menuBar: content.item ? content.item.menuBar : null
    minimumHeight: isMobile ? 0 : 300
    minimumWidth: isMobile ? 0 : 680
    visible: true
    width: isMobile ? Screen.width : designWidth

    function updateVisibility() {
        if (!Mixxx.Core.ready) {
            return;
        }
        root.visibility = Mixxx.Config.configStartInFullscreenKey || isMobile
                ? Window.FullScreen
                : Window.Windowed;
    }

    function selectedSkinName() {
        if (typeof NraveResolvedSkinName !== "undefined" && NraveResolvedSkinName !== "") {
            return NraveResolvedSkinName;
        }
        return Mixxx.Config.configSkin || "AndroidDefault";
    }

    function selectedMainWindowUrl() {
        if (Qt.platform.os === "android" &&
                typeof NraveResolvedSkinMainWindowUrl !== "undefined" &&
                NraveResolvedSkinMainWindowUrl !== "") {
            return NraveResolvedSkinMainWindowUrl;
        }
        const selectedSkin = root.selectedSkinName();
        if (selectedSkin === "" || selectedSkin === "AndroidDefault") {
            return Qt.resolvedUrl("MainWindow.qml");
        }
        return Qt.resolvedUrl("../skins/" + selectedSkin + "/MainWindow.qml");
    }

    function loadSelectedMainWindow() {
        const sourceUrl = root.selectedMainWindowUrl();
        console.warn("NRAVE_QML_SHELL_LOADING_SKIN", root.selectedSkinName(), sourceUrl);
        content.setSource(sourceUrl, { "applicationWindow": root });
    }

    Connections {
        target: Mixxx.Core
        function onReadyChanged() {
            root.updateVisibility();
        }
    }

    Component.onCompleted: root.updateVisibility()

    Loader {
        id: content
        anchors.fill: parent
        active: Mixxx.Core.ready
        asynchronous: true
        onActiveChanged: {
            if (active) {
                root.loadSelectedMainWindow();
            }
        }
        onStatusChanged: {
            if (status === Loader.Ready) {
                console.warn("NRAVE_QML_SHELL_SKIN_READY", root.selectedSkinName(), source)
                return
            }
            if (status === Loader.Error) {
                console.error("NRAVE_QML_SHELL_SKIN_ERROR", root.selectedSkinName(), source)
            }
        }
    }

    Loader {
        id: abletonLinkOverlay
        anchors.fill: parent
        active: root.isMobile && Mixxx.Core.ready && content.status === Loader.Ready
        asynchronous: false
        z: 100001
        source: "AbletonLinkOverlay.qml"
    }

    Loader {
        id: bitgridOverlay
        anchors.fill: parent
        active: root.isMobile && Mixxx.Core.ready && content.status === Loader.Ready
        asynchronous: false
        z: 100000
        source: "BitGridOverlay.qml"
    }

    Rectangle {
        id: splash
        visible: opacity > 0
        color: Theme.backgroundColor
        anchors.fill: parent

        property bool ready: false

        Component.onCompleted: ready = true

        states: [
            State {
                when: splash.ready && content.status != Loader.Ready
                PropertyChanges {
                    text.opacity: 1
                    logo.opacity: 1
                    logo.y: root.height / 2 - logo.height / 2
                }
            },
            State {
                when: content.status === Loader.Ready && content.active
                PropertyChanges {
                    splash.opacity: 0
                }
            }
        ]

        Image {
            id: logo
            anchors.horizontalCenter: parent.horizontalCenter
            source: "qrc:/images/mixxx-icon-logo-symbolic.svg"
            opacity: 0
            y: root.height / 2
            Behavior on opacity {
                NumberAnimation { duration: 1500; easing.type: Easing.InOutQuad }
            }
            Behavior on y {
                NumberAnimation { duration: 1500; easing.type: Easing.InOutQuad }
            }
        }

        Text {
            id: text
            opacity: 0
            y: logo.y + logo.height * 2
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.topMargin: 20
            font.pixelSize: 12
            color: Theme.lightGray3
            text: "DJ your way"
            Behavior on opacity {
                SequentialAnimation {
                    PauseAnimation { duration: 1000 }
                    NumberAnimation { duration: 500; easing.type: Easing.InOutQuad }
                }
            }
        }

        Behavior on opacity {
            NumberAnimation { duration: 500; easing.type: Easing.InOutQuad }
        }
    }
}